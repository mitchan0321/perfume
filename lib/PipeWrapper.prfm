# Class name: PipeWrapper
#
# Description:
#    This class is pipe drivered class.
#
#    Impliment methods are:
#      PipeWrapper::init
#      

class PipeWrapper;

PipeWrapper method init (in out body) {
    sets in $in;
    sets out $out;
    sets body $body;
    sets stat INIT;
    sets infile "";
    sets outfile "";
    sets error "";
    sets result nil;
    sets PipeObject [self];
    sets @term-hook nil;
};

PipeWrapper method stat? () {
    return $stat;
};

PipeWrapper method run () {
    if {sets? coro} else: {
	sets coro [coro $body];
	$coro eval {
	    setvar stdin $in;
	    setvar stdout $out;
	    setvar INFILE $infile;
	    setvar OUTFILE $outfile;
	    if {defvar? CO-MODE} else: {defvar CO-MODE t};
	};
    };

    sets stat RUN;
    try {
	sets stat [$coro next];
	if {eq? [$coro stat] DONE} then: {
	    sets stat DONE;
	};
    }
    catch: {| e |
	sets stat ERROR;
	sets error ["<" . [$e car] "># " [$e cdr]];
    };
    
    if {or [eq? $stat DONE] [eq? $stat ERROR]} then: {
	$out flush;
	if {eq? $out,@name Stream} then: {$out close;};
	if {eq? $in,@name  Stream} then: {$in  close;};
    };
};

PipeWrapper method debug_out () {
    println $in;
    println $body;
    println $out;
};

PipeWrapper method string () {
    "PipeWrapper::" . $body " | " $in " | " $out;
};

PipeWrapper method release () {
    if {sets? coro} then: {
	$coro release;
    };
    if [true? @term-hook] then: {$@term-hook};
};

PipeWrapper method set-infile (f) {
    sets infile $f;
};

PipeWrapper method set-outfile (f) {
    sets outfile $f;
};

PipeWrapper method get-body () {
    return $body;
};

PipeWrapper method get-error () {
    return $error;
};

PipeWrapper method get-result () {
    return $result;
};

PipeWrapper method set-result (r) {
    sets result $r;
};

PipeWrapper method out-setup () {
    o ::= make-proxy $out;
    f ::= $o method? puts;
    s ::= self;
    
    $o method puts (*) {
	r ::= call $f $*;

	if {[$* len] = 1} then: {
	    $s set-result [$* item];
	} else: {
	    $s set-result $*;
        };

	return $r;
    };

    sets out $o;
};

PipeWrapper method set-term-hook (h) {
    sets @term-hook $h;
};
