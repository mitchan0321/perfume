defun prime-fact (n) {
    s ::= dict;
    prime-fact-sub $n $s;

    result := ();
    lsort [$s keys :string : map {| i | $i int}] : each do: {| i |
        $result <<- ($i $s,$i);
    };
    return $result;
};

defun prime-fact-sub (n s) {
    f := 2;
    t ::= $n sqrt;
    while {$f <= $t} do: {
        if {[$n % $f] = 0}
        then: {
            add-fact $f $s;
            goto prime-fact-sub [$n / $f] $s;
        };
        f ::= nextprime* $f;
    };
    add-fact $n $s;
};

defun nextprime* (n) {
    $n nextprime;
};
memoiz nextprime*;

defun add-fact (n s) {
    if [$s set? $n]
    then: {$s set $n [$s,$n + 1]}
    else: {$s set $n 1}
    ;
};

defun prime-fact-check (l) {
    $l inject 1 do: {| s i | $s * [$i,0 ^ $i,1]};
};

defun prime-fact-test (n) {
    1 each to: $n do: {| i |
        f ::= prime-fact $i;
        r ::= prime-fact-check $f;
        println $i ", " $f ", " [if [$i = $r] "OK" "*** NG ***"];
    };
};
