class BufferManager;

BufferManager method init () {
    [sets? BufferList] ?? {sets BufferList [dict ()]};
    [sets? BufferGeom] ?? {sets BufferGeom [dict ()]};
};

BufferManager method regist-buffer (buff name) {
    i := 1;
    name-orig := $name;
    while {$BufferList set? $name} do: {
	$i ++;
	name ::= $name-orig . "[" $i "]";
    };
    $buff set-buffer-name $name;
    $BufferList set $name $buff;
};

BufferManager method get-buffer (name) {
    return [$BufferList get $name];
};

BufferManager method get-next-buffer (buff) {
    l ::= msort [$BufferList keys :string];
    i ::= $l find [$buff get-buffer-name];
    r ::= $l get [$i + 1];
    $BufferList get [$r ?? $l,0];
};

BufferManager method set-buffer-geom (window-name buffer-name cursor-y cursor-x) {

    [$BufferGeom set? ($window-name $buffer-name)] ?? {
	$BufferGeom each do: {| k v |
	    (w b g) := $v;
	    if {$b = $buffer-name} {
		geom ::= $g clone;
		$geom set! CursorLine $cursor-y;
		$geom set! CursorColumn $cursor-x;
		$BufferGeom set
		    ($window-name $buffer-name)
		    ($window-name $buffer-name $geom);
		return;
	    };
	};

	$BufferGeom set
	    ($window-name $buffer-name)
	    ($window-name $buffer-name [new BufferGeom]);
	return;
    };

    (_dumy _dumy geom) ::= $BufferGeom get ($window-name $buffer-name);
    $geom set! CursorLine $cursor-y;
    $geom set! CursorColumn $cursor-x;
    $BufferGeom set
	($window-name $buffer-name)
	($window-name $buffer-name $geom);
};

BufferManager method get-buffer-geom (window-name buffer-name) {
    geom ::= $BufferGeom get ($window-name $buffer-name);
    if $geom {
	return $geom,2;
    };
    return [new BufferGeom];
};
