class Task;

Task method init () {
    sets Name "";
    sets Coro <nil>;
    sets Last 0;
    sets Sleep 0;
    sets Elapsed 0;
    sets ExitStatus 0;

    #
    # Task status:
    #   I ... Initial
    #   A ... Active
    #   R ... Running
    #   S ... Sleep
    #   X ... eXit
    #   W ... Wait
    sets State I;
};

Task method set-name (name) {
    sets Name $name;
};

Task method get-name () {
    return $Name;
};

Task method proc (name body args: arg) {
    sets Coro [coro $body];
    $Coro eval {
        defvar ARGS $arg;
        defvar SELF [self];
        enable-itimer;
    };
    TaskManager add [self] $name;
    sets State A;
};

Task method set-stdio (in out err) {
    $Coro ?? {return};

    sets __in $in;
    sets __out $out;
    sets __err $err;
    $Coro eval {
        setvar stdin $__in;
        setvar stdout $__out;
        setvar stderr $__err;
    };
};

Task method release () {
    if $Coro {$Coro release};
};

Task method time-elapsed (msec) {
    $Sleep -- $msec;
};

Task method run () {
    sets State R;
    b ::= time-of-day :msec;
    try {unset e} catch: {};
    try {sts ::= $Coro next} catch: {| e |};
    $Elapsed ++ [[time-of-day :msec] - $b];

    if {set? e} {
        sets State X;
        sets ExitStatus (ERROR $e);
        release;
        return;
    };
    if {intr? $sts} {
        sets State A;
        return;
    };
    if {eq? [$Coro stat] DONE} {
        sets State X;
        sets ExitStatus 0;
        release;
        return;
    };

    if {list? $sts} {
        case [$sts car]
        SLEEP {
            sets State S;
            sets Sleep [$sts get 1];
        }
        EXIT {
            sets State X;
            sets ExitStatus [$sts get 1];
        }
        default {
            State A;
        }
        ;;
        return;
    } {
        case $sts
        @READ {
            sets State W;
        }
        @WRITE {
            sets State W;
        }
        default: {
            sets State A;
        }
    };
};

Task method tickable? () {
    if {or {eq? $State A} {eq? $State S}} <t> <nil>;
};
