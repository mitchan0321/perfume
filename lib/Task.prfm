class Task;

Task method init () {
    sets Name "";
    sets Coro <nil>;
    sets Last 0;
    sets Sleep 0;
    sets Elapsed 0;
    sets ExitStatus 0;

    #
    # Task status:
    #   I ... Initial
    #   A ... Active
    #   R ... Running
    #   S ... Sleep
    #   X ... eXit
    #   W ... Wait
    sets State I;
};

Task method set-name (name) {
    sets Name $name;
};

Task method get-name () {
    return $Name;
};

Task method proc (name body args: arg) {
    TaskManager add [self] $name;
    sets Coro [coro $body];
    sets State A;
};

Task method release () {
    if $Coro {$Coro release};
};

Task method time-elapsed (msec) {
    $Sleep -- $msec;
};

Task method run () {
    sets State R;
    b :: = time-of-day :msec;
    sts ::= $Coro next;
    $Elapsed ++ [[time-of-day :msec] - $b];

    if {intr? $sts} {
        sets State A;
        return;
    };
    if {eq? [$Coro stat] DONE} {
        sets State X;
        return;
    };

    if {list? $sts} {
        case [$sts car]
        PAUSE {
            sets State S;
            sets Sleep [$sts get 1];
        }
        EXIT {
            sets State X;
            sets ExitStatus [$sts get 1];
        }
        default {
            State A;
        }
        ;;
        return;
    } {
        case $sts
        @READ {
            sets State W;
        }
        @WRITE {
            sets State W;
        }
        default: {
            sets State A;
        }
    };
};
