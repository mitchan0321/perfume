defun chat (host) {
    chat-config;

    f ::= connect [resolv-host $host] $CHAT-PORT;
    server ::= new File;
    $server set! $f mode: io;
    $server set-encoding UTF-8;
    $server set-newline <t>;
    $server set-ignore-cr <t>;

    try {
        loop do: {
	    if {$stdin eof?} {
		println "Chat client exit.";
		return;
	    };
	    if {$stdin ready? timeout: 100} {
	        message ::= $stdin gets;
	        if [or [$stdin eof?] [nil? $message]] then: {
		    println "Chat client exit.";
		    return;
		};
		$server puts [$message . "\r"];
                $server flush;
	    };
	    if {$server ready?} {
	        message ::= $server gets;
	        if [or [$server eof?] [nil? $message]] then: {
		    println "Chat server down.";
		    return;
		};
		println [$message clean];
	    };
	};
    }
    fin: {
	$server close;
    };
};
