defun chat (host) {
    chat-config;

    f ::= connect [resolv-host $host] $CHAT-PORT;
    server ::= new File;
    $server set! $f mode: io;
    $server set-encoding UTF-8;
    $server set-newline <t>;
#    println "*** " [$server stat];

    try {
        loop do: {
            (ifds ofds efds) ::= select ([$stdin fd?] [$server fd?]) () () 100;
	    if {$ifds null?} else: {
		$ifds each do: {| fds |
		    cond
		    {$fds = [$stdin fd?]} {
			message ::= $stdin gets;
			if [or [$stdin eof?] [nil? $message]] then: {
			    println "Chat client exit.";
			    return;
			};
			$server puts [$message clean];
		    }
		    {$fds = [$server fd?]} {
			message ::= $server gets;
			if [or [$server eof?] [nil? $message]] then: {
			    println "Chat server down.";
			    return;
			};
			println [$message clean];
		    };
		};
	    };
	};
    }
    fin: {
	$server close;
    };
};
