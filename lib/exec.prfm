# exec external process

load-class PollingManager;

defun exec (args: args) {
    args ::= $args map {| i | "" . $i};
    command ::= $args >>;

    d ::= dict [fork-exec $command $args];

    pid := $d,pid;

    out := $d,left;
    OUT ::= new File;
    $OUT set! $out mode: o;
    $OUT set-newline <t>;
    $OUT set-nobuffer;

    in := $d,right;
    IN ::= new File;
    $IN set! $in mode: i;
    $IN set-newline <nil>;
    $IN set-nobuffer;
    if {true? CO-MODE} then: {$IN set-noblock t};

    err := $d,error;
    ERR ::= new File;
    $ERR set! $err mode: i;
    $ERR set-newline <nil>;
    $ERR set-nobuffer;
    if {true? CO-MODE} then: {$ERR set-noblock t};

    if {true? PipeObject} then: {
	$PipeObject set-term-hook [fun () {$IN close; $OUT close; $ERR close}];
    };

    status := 255;
    try {
	poll ::= new PollingManager;

	$poll set-exit-condition-block {
	    result [and [$IN eof?] [$ERR eof?]];
	};

	$poll set-wait-block {
	    if [$stdin eof?] {$OUT close};
	    sts := <nil>;
	    if {and [$stdin ready?] [$OUT ready?]} {
		sts := <t>;
	    } {
		try {
		    (in out exp) ::= select ([$IN fd?] [$ERR fd?]) () () 50;
		    if {$in null?} {
		        sts := <nil>;
		    } {
		        sts := <t>;
		    };
		} catch: {
		    sts := <nil>;
		};
	    };
	    result $sts;
	};
	
	$poll add-event-queue (
	    {and [$stdin ready?] [$OUT ready?]} {
		i ::= $stdin gets;
		if $i {
		    $OUT puts $i;
		    $OUT flush;
		} {
		    $OUT close;
		};
	    }
	);
	
	$poll add-event-queue (
	    {$IN ready?} {
		i := <nil>;
		try {
		    i ::= $IN gets;
		} catch: {| e |
		    if {eq? [$e car] ErrIOAgain} {
			if {true? CO-MODE} {
			    pause @WRITE;
			    continue;
			} {
			    throw $e;
			};
		    } {
			throw $e;
		    };
		};
		if $i {
		    $stdout puts $i;
		    $stdout flush;
		};
	    }
	);
	
	$poll add-event-queue (
	    {$ERR ready?} {
		i := <nil>;
		try {
		    i ::= $ERR gets;
		} catch: {| e |
		    if {eq? [$e car] ErrIOAgain} {
			if {true? CO-MODE} {
			    pause @WRITE;
			    continue;
			} {
			    throw $e;
			};
		    } {
			throw $e;
		    };
		};
		if $i {
		    $stderr puts $i;
		    $stderr flush;
		};
	    }
	);
	
	$poll event-loop;
    }
    
    fin: {
	try {$OUT close} catch: {};
	try {$IN  close} catch: {};
	try {$ERR close} catch: {};
	status ::= wait $pid;
	if {$status = 255} then: {
	    throw ErrExecProgram "Can\'t execute program."
	};
    };

    return $status;
};
