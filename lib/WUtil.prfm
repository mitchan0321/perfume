class WUtil;

WUtil method init () {
    # init config DB
    undef CONFIG;
    undef CONFIG_DESC;
    undef CONFIG_TYPE;
    undef CONFIG_RO;
    defvar CONFIG [dict];
    defvar CONFIG_DESC [dict];
    defvar CONFIG_TYPE [dict];
    defvar CONFIG_RO [dict];
    
    init-config;
    init-command-DB;
    load-keydesc;

    # Ommit exit command.
    d ::= dict-func;
    f ::= $d get exit;

    $d unset exit;
    $d set _exit $f;
    defun exit () {$MainWindow exit-event-loop};

    if {! [defvar? CopyBuffer]} {
	defvar CopyBuffer "";
    };
    if {! [defvar? SearchString]} {
	defvar SearchString "";
    };
};

WUtil method term () {
    d ::= dict-func;
    f ::= $d get _exit;
    $d unset exit;
    $d set exit $f;
};

WUtil method color-pair (fg bg) {
    if {sets? ColorPair} else: {
	sets ColorPair [dict];
	$ColorPair set count 0;
    };
    if {sets? ColorName} else: {
	sets ColorName [dict];
	$ColorName set BLACK   0;
	$ColorName set RED     1;
	$ColorName set GREEN   2;
	$ColorName set YELLOW  3;
	$ColorName set BLUE    4;
	$ColorName set MAGENTA 5;
	$ColorName set CYAN    6;
	$ColorName set WHITE   7;
    };

    if {$ColorName set? $fg} else: {throw ErrNoSuchColorName "No such foreground color name."};
    if {$ColorName set? $bg} else: {throw ErrNoSuchColorName "No such background color name."};    

    if {$ColorPair set? ($ColorName,$fg $ColorName,$bg)}
    then: {
	return [$ColorPair get ($ColorName,$fg $ColorName,$bg)];
    }
    else: {
	p ::= $ColorPair get count;
	p ::= $p + 1;
	curs-add-color $p $ColorName,$fg $ColorName,$bg;
	$ColorPair set ($ColorName,$fg $ColorName,$bg) $p;
	$ColorPair set $p ($ColorName,$fg $ColorName,$bg);
	$ColorPair set count $p;
	return $p;
    };
};

WUtil method get-bgcolor (name) {
    n ::= $ColorDB get $name;
    $n ?? {throw ErrNoSuchColor ["WUtil::get-bgcolor: No such color DB name, " . $name]};
    try {
	(fg bg) ::= $ColorPair get $n;
    }
    catch: {throw ErrNoSuchColor ["WUtil::get-bgcolor: No color pair no, " . $n]};
    return $bg;
};

WUtil method get-fgcolor (name) {
    n ::= $ColorDB get $name;
    $n ?? {throw ErrNoSuchColor ["WUtil::get-fgcolor: No such color DB name, " . $name]};
    try {
	(fg bg) ::= $ColorPair get $n;
    }
    catch: {throw ErrNoSuchColor ["WUtil::get-fgcolor: No color pair no, " . $n]};
    return $fg;
};

defun init-config () {
    if {defvar? PMACS_INIT}
    else: {
	$LIB_PATH each do: {| i |
	    if {file exists? [$i . "/pmacs.conf"]} {
	        load :notrace [$i . "/pmacs.conf"];
		break;
	    };
	};
	defvar PMACS_INIT <t>;
    };
};

defun load-keydesc () {
    $LIB_PATH each do: {| i |
        if {file exists? [$i . "/pmacs-keydesc.prfm"]} {
            load :notrace [$i . "/pmacs-keydesc.prfm"];
            break;
        };
    };
};

defun init-color-DB () {
    (
        (A_ALTCHARSET   0x00400000 "代替文字セットモード")
        (A_BLINK        0x00080000 "点滅モード")
        (A_BOLD         0x00200000 "太字モード")
        (A_DIM          0x00100000 "低輝度モード")
        (A_INVIS        0x00800000 "Invisible or blank mode")
        (A_ITALIC       0x80000000 "イタリックモード")
        (A_NORMAL       0x00000000 "通常の属性")
        (A_PROTECT      0x01000000 "Protected mode")
        (A_REVERSE      0x00040000 "Reverse background and foreground colors")
        (A_STANDOUT     0x00010000 "強調モード")
        (A_UNDERLINE    0x00020000 "下線モード")
        (A_HORIZONTAL   0x02000000 "Horizontal highlight")
        (A_LEFT         0x04000000 "Left highlight")
        (A_LOW          0x08000000 "Low highlight")
        (A_RIGHT        0x10000000 "Right highlight")
        (A_TOP          0x20000000 "Top highlight")
        (A_VERTICAL     0x40000000 "Vertical highlight")
        (A_CHARTEXT     0x000000ff "Bit-mask to extract a character")
    ) each do: {| i |
        (a v d) := $i;
        if [defvar? $a] else: {
            defvar $a $v;
        };
    };
    if [defvar? AttrDB] else: {
	defvar AttrDB [dict];
    };
    
    if [defvar? ColorDB] else: {
	defvar ColorDB [dict];
    };

    $LIB_PATH each do: {| i |
	if {file exists? [$i . "/pmacs-theme.prfm"]} {
	    load :notrace [$i . "/pmacs-theme.prfm"];
	    break;
	};
    };
    read-user-conf-file "theme.conf";
};

defun set-color-name (name color-pair-no attr: attr-list) {
    $ColorDB set $name $color-pair-no;

    if [set? attr-list] {
        attr := 0;
        $attr-list each do: {| i |
            attr ::= $attr || $i;
        };
        $AttrDB set $name $attr;
    } {
        $AttrDB unset $name;
    };

    true;
};

defun apply-color (curs y x len color-name) {
    attr := $AttrDB,$color-name;
    if $attr {
        curs-color $curs $y $x $len $ColorDB,$color-name $attr;
    } {
        curs-color $curs $y $x $len $ColorDB,$color-name 0;
    };
};

defun init-command-DB () {
    if {defvar? EditorCommand}
    else: {
	defvar EditorCommand [dict];
    };
    if {defvar? EditorCommandDesc}
    else: {
	defvar EditorCommandDesc [dict];
    };

    $LIB_PATH each do: {| i |
	if {file exists? [$i . "/pmacs-command.prfm"]} {
	    load [$i . "/pmacs-command.prfm"];
	    break;
	};
    };
};

defun defcommand (name block desc: desc) {
    $EditorCommand set $name $block;
    $EditorCommandDesc set $name [[set? desc] ??? {result $desc} ""];
};

defun get-command (name) {
    $EditorCommand get $name;
};

defun get-command-desc (name) {
    $EditorCommandDesc get $name;
};

defun set-window-color-default (curs name) {
    curs-set-bgcolor $curs [$ColorDB get $name];
    curs-set-color   $curs [$ColorDB get $name];
};

defun line-input (curs disp-line disp-col input-len cur-index cur-val key-in) {
    ### curs-print $curs ["%-" . $input-len "v" : fmt ""] $DISPLAY_ENCODING $disp-line $disp-col;
    curs-render-line $curs $disp-line 0 $disp-col $cur-val $DEFAULT_TAB_WIDTH $DISPLAY_ENCODING;
    v ::= vector($cur-val);
    new-x := $cur-index;
    result := SELF;
    cond
        {string? $key-in} {
            curs-x ::= curs-index-to-pos $cur-val [$cur-val len] $DEFAULT_TAB_WIDTH;
            if {$curs-x < $input-len} {
                (command start-y start-x new-y new-x) ::= $v insert-string-at $key-in 0 $cur-index;
            };
            break;
        }
        {eq? $key-in KEY_RIGHT} {
            new-x ::= $cur-index + 1;
            if {$new-x > [$v get 0 : len]} {
                new-x := [$v get 0 : len];
            };
            break;
        }
        {eq? $key-in KEY_LEFT} {
            new-x ::= $cur-index - 1;
            if {$new-x < 0} {new-x := 0};
            break;
        }
        {eq? $key-in kRIT5} {
            new-x ::= $cur-val len;
            break;
        }
        {eq? $key-in kLFT5} {
            new-x := 0;
            break;
        }
        {eq? $key-in KEY_END} {
            new-x ::= $cur-val len;
            break;
        }
        {eq? $key-in KEY_HOME} {
            new-x := 0;
            break;
        }
        {or [eq? $key-in KEY_BACKSPACE] [eq? $key-in CTRL_H] [eq? $key-in KEY_DEL]} {
            (command char new-y new-x) ::= $v backdelete-character-at 0 $cur-index;
            break;
        }
        {or [eq? $key-in KEY_DC] [eq? $key-in CTRL_D]} {
            (command char new-y new-x) ::= $v delete-character-at 0 $cur-index;
            break;
        }
        {eq? $key-in kDC5} {
            (command char new-y new-x) ::= $v delete-region 0 0 0 [$cur-val len];
            break;
        }
        {eq? $key-in CTRL_V} {
            if {$CopyBuffer = ""} {break};
            (command start-y start-x new-y new-x) ::= $v insert-string-at $CopyBuffer 0 $cur-index;
            break;
        }
        {eq? $key-in kDC8} {
            $MainWindow mini-input-end;
            break;
        }
        {eq? $key-in CTRL_G} {
            $MainWindow send-key-event (KEY_ESC);
            break;
        }
    ;;

    return ($key-in $new-x [if {[$v len] = 0} "" {$v get 0}] $result);
};

defun string-width (str tab-width) {
    return [curs-index-to-pos $str [$str len] $tab-width];
};

defun show-message (
    wait: msec 
    bell: bell
    debug: debug
    log: log
    args: msgl
) {
    msg ::= $msgl join sep: "";

    if {set? log} {
        msg ::= "Log: " . $msg;
        try {
            TaskManager output-console [">>>" . $msg];
        } catch: { ### do nothing
        };
        return;
    };
    
    if {set? debug} {
        msg ::= "Debug: " . $msg;
        try {
            TaskManager output-console [">>>" . $msg];
        } catch: { ### do nothing
        };
    };
    
    if {defvar? MainWindow} {
	try {
            [$MainWindow get-minibuffer-window] ?? {throw ErrNoMiniBuffer "No MiniBuffer for debugging."};
            $MainWindow display-window-message "MiniBuffer" $msg;
            if [set? bell] {
                $MainWindow get-minibuffer-window : 
                    set-color
                        0 0
                        [$MainWindow get-screen-size : get 1]
                        MINI_CAUTION 0;
            } {
                $MainWindow get-minibuffer-window : 
                    set-color
                        0 0
                        [$MainWindow get-screen-size : get 1]
                        MINI_WINDOW 0;
            };
            $MainWindow get-minibuffer-window : refresh;
            if [set? bell] {
                curs-flash;
            };
            if {set? msec} {sleep $msec};
	} catch: {};
    } {
        println file: $stderr $msg;
    };
};

defun path-adjust (path) {
    l ::= $path split sep: "/";
    r := ();
    $l each do: {| i |
	cond
	    {$i = ".."} {if {[$r len] > 1} {$r ->>}; break}
	    {$i = "."}  {break}
	    {$i = ""}   {if {[$r len] > 1} {break}}
	    <t>         {$r <<- $i}
	;
    };
    if {and {[$r len] = 1} {[$r get 0] = ""}} {return "/"};
    return [$r join sep: "/"];
};

defun get-dir (path) {
    l ::= $path split sep: "/";
    $l ->>;
    result ::= path-adjust [$l join sep: "/"];
    if {$result != "/"} {
        $result append! "/";
    };
    return $result;
};

defun show-files-in-window (main window dir) {
    BufferManager unregist-buffer "*Completion*";
    buff ::= new EditBuffer;
    $buff load-content [$dir . "/"] :init-only buffer-name: "*Completion*";
    $buff set-raise-low;
    $buff clear-all;

    if {file dir? $dir} {

	### display directory

	dir ::= path-adjust $dir;
	if {$dir != "/"} {dir ::= $dir . "/"};
	$buff append-string ["<<< In directory: %v >>>" fmt $dir];
	$buff append-string "";

	try {
	    l ::= msort [file list $dir];
	    while {! [$l null?]} do: {
		v1 ::= $l >>;
		if {and $v1 {file dir? [$dir . $v1]}} {$v1 append! "/"};
		v2 ::= $l >>;
		if {and $v2 {file dir? [$dir . $v2]}} {$v2 append! "/"};
		# v3 ::= $l >>;
		# if {and $v3 {file dir? [$dir . $v3]}} {$v3 append! "/"};		
		# v4 ::= $l >>;
		# if {and $v4 {file dir? [$dir . $v4]}} {$v4 append! "/"};
		# $buff append-string ["%-20v\t%-20v\t%-20v\t%-20v" fmt [$v1 ?? ""] [$v2 ?? ""] [$v3 ?? ""] [$v4 ?? ""]];
		$buff append-string ["%-40w%-40w" fmt [$v1 ?? ""] [$v2 ?? ""]];
	    };
	} catch: {| e |
	    $buff append-string ["Can't read directory. <" [$e car] "># " [$e cdr]];
	};
    } {

	### display files and mathchies

	flist ::= path-adjust $dir : split sep: "/";
	fname ::= $flist ->>;

	try {
	    dir ::= $flist join sep: "/";
	    if {$dir != "/"} {dir ::= $dir . "/"};
	    $buff append-string ["<<< In directory matchies: %v >>>" fmt $dir];
	    $buff append-string "";
	
	    l ::= file list $dir : filter {| i |
                ### $i =~ ["^" . $fname : rquote];
	        ### $i =~ :text [$fname rquote];
                [$i sub 0 [$fname len]] = $fname;
	    };
	    l ::= msort $l;
	    src-list ::= $l clone;
	    file-count ::= $l len;
	    if {$file-count >= 1} {
		while {! [$l null?]} do: {
		    v1 ::= $l >>;
		    if {and $v1 {file dir? [$dir . $v1]}} {$v1 append! "/"};
		    v2 ::= $l >>;
		    if {and $v2 {file dir? [$dir . $v2]}} {$v2 append! "/"};
		    # v3 ::= $l >>;
		    # if {and $v3 {file dir? [$dir . $v3]}} {$v3 append! "/"};		
		    # v4 ::= $l >>;
		    # if {and $v4 {file dir? [$dir . $v4]}} {$v4 append! "/"};
		    # $buff append-string ["%-20v\t%-20v\t%-20v\t%-20v" fmt [$v1 ?? ""] [$v2 ?? ""] [$v3 ?? ""] [$v4 ?? ""]];
		    $buff append-string ["%-40w%-40w" fmt [$v1 ?? ""] [$v2 ?? ""]];
		};
		if {$file-count = 1} {
		    $buff append-string "--";
		    $buff append-string "File match solo complete.";
		    fname := $v1;
		} { 
		    $buff append-string "--";
		    $buff append-string ["%v files match." fmt $file-count];
		    fname ::= path-longest-match $src-list $fname;
		};
	    } {
		$buff append-string "--";
		$buff append-string "No file match.";
	    };

	    dir ::= $dir . $fname;

	} catch: {| e |
	    $buff append-string ["Can't read directory. <" [$e car] "># " [$e cdr]];
	};
    };

    $buff no-modify;
    $main bind-buffer [$window get-window-name] $buff;
    return $dir;
};

defun path-longest-match (list fname) {
    if {$list null?} {return ""};
    if {$list len : = 1} {return [$list car]};

    result := $fname;
    from ::= $fname len;
    to ::= $list map {| i | $i len} : min : - 1;
    $from each to: $to do: {| i |
	m ::= $list car : at $i;
	match := <t>;
	[$list cdr] each do: {| x |
	    if {$m != [$x at $i]} {
	        match := <nil>;
		break;
	    };
	};
	if $match {
            $result append! $m
        } {
	    break;
	};
    };
    
    return $result;
};

defun path-match (list fname) {
    result := ();
    l := $result;
    $list each do: {| i |
        if {$i =~ ["^" . $fname : rquote]} {
            l ::= $l append! $i;
        };
    };
    return $result;
};

##
## define Vector method for EditBuffer operations.
##
Vector method insert-string-at-next (char line at) {
    (command ay ax by bx) ::= insert-string-at $char $line $at;
    return ($command $ay $ax $by $bx);
};

Vector method insert-string-at (str line at) {
    if {$str = ""} {
        if {$line >= [self : len]} {self : append! ""};
        return (delete-region $line $at $line $at);
    };
    
    l ::= $str split sep: "\r";
    times ::= $l len;
    self ::= self;
    i := 1;
    result := (delete-region [$line + 0] [$at + 0]);
    $l each do: {| elem |
	if {$line < [$self len]} {
	    s ::= $self get $line;
	    if {$at > [$s len]} {at := [$s len]};
	    s1 ::= $s sub 0 $at;
	    s2 ::= $s sub $at;
	    $self set $line [$s1 . $elem $s2];
	    $at ++ [$elem len];
	} {
	    $self append! $elem;
	    at ::= $elem len;
	    line ::= $self len : - 1;
	};
	if {$i = $times} {break};
	#$self append! "";
	#[$self len : - 2] each to: $line do: {
	#    | j |
	#    if {$line = $j} {
	#	s1 ::= $self get $j : sub 0 $at;
	#	s2 ::= $self get $j : sub $at;
	#	$self set [$j + 1] $s2;
	#	$self set $j $s1;
	#    } {
	#	$self set [$j + 1] [$self get $j];
	#    };
	#};
        $self insert! $line "";
        s1 ::= $self get [$line + 1] : sub 0 $at;
        s2 ::= $self get [$line + 1] : sub $at;
        $self set [$line + 1] $s2;
        $self set [$line - 0] $s1;
        
        $i ++;
	$line ++;
	at := 0;
    };
    $result <<- [$line + 0];
    $result <<- [$at + 0];
    return $result;
};

Vector method delete-character-at (line at) {
    self ::= self;
    if {$line >= [$self len]} {
	if {[$self len] = 0} {return (<nil> "" 0 0)};
	return (<nil> "" [$self len] 0);
    };
    s ::= $self get $line;
    len ::= $s len;
    if {and {[$self len] = 1} {$len = 0}} {
	$self resize 0;
	return (insert-string-at "" 0 0);
    };
    if {$len = 0} {
        #i ::= $line + 1;
        #while {$i < [$self len : - 1]} do: {
	#    $self set $i [$self get [$i + 1]];
	#    i ::= $i + 1;
        #};
        #$self resize [$self len : - 1];
        if {$line = [$self len : - 1]} {
            $self resize [$self len : - 1];
        } {
            $self set [$line + 0] [$self delete! [$line + 1]];
        };
        if {$line >= [$self len]} {
            return (insert-string-at "" $line 0);
        };
	return (insert-string-at "\r" $line 0);
    };
    if {$at >= $len} {
	if {[$line + 1] = [$self len]} {
	    return (insert-string-at "" $line $len);
	};
	$self set $line [$self,$line . [$self get [$line + 1]]];
	if {$line = [$self len : - 2]} {
            $self resize [$self len : - 1];
            if {$line >= [$self len]} {
                return (insert-string-at "" $line 0);
            };
	    return (insert-string-at "\r" $line $len);
	};
        #[$line + 1] each to: [$self len : - 2] do: {| i |
	#    $self set $i [$self get [$i + 1]];
	#};
	#$self resize [$self len : - 1];
        $self delete! [$line + 1];
        if {$line >= [$self len]} {
            return (insert-string-at "" $line 0);
        };
	return (insert-string-at "\r" $line $len);
    };
    s1 ::= $s sub 0 $at;
    c ::= $s sub $at [$at + 1];
    s2 ::= $s sub [$at + 1];
    $self set $line [$s1 . $s2];
    return (insert-string-at $c $line $at);
};

Vector method backdelete-character-at (line at) {
    self ::= self;
    if {and {$line >= 1} {$line >= [$self len]}} {
        if {[$self get [$line - 1]] != ""} {
            return (insert-string-at-next "" [$line - 1] [$self get [$line - 1] : len]);
        };
    };
    if {$at = 0} {
	if {$line = 0} {
	    return (<nil> "" 0 0);
	};
	line ::= $line - 1;
	at ::= [$self get $line] len;
	(command char line at) ::= delete-character-at $line $at;
        if {$line >= [$self len]} {
            return (insert-string-at-next "" $line 0);
        };
	return (insert-string-at-next "\r" $line $at);
    };
    at ::= $at - 1;
    (command char line at) ::= delete-character-at $line $at;
    return (insert-string-at-next $char $line $at);
};

##
## This method (Vector::append-string) same as following this:
## if [$Src len] > 0
## then
##   Vector:: insert-string-at
##	["\r" . $str]
##	[[self] len : - 1]
##	[[self] get [$Src len : -1] : len]
## else
##   Vector:: insert-string-at
##     $str 0 0
##
Vector method append-string (str) {
    [self] append! $str;
    if {[self : len] > 1} {
	return (
	    delete-region
	      [self : len : - 2] [self : get [self : len : - 2] : len]
	      [self : len : - 1] [self : get [self : len : - 1] : len]
	    );
    } {
	return (
	    delete-region
	      0 0 0 [self : get 0 : len]
	    );
    };
};

Vector method line-update (str y) {
    if {$y >= [self : len]} {return (<nil> 0 0)};
    
    r ::= [self] get $y;
    [self] set $y $str;
    result := (line-update $r $y);
    return $result;
};

Vector method delete-region (start-line start-column end-line end-column) {
    if {$start-line > $end-line} {
	(start-line end-line) := ($end-line $start-line);
	(start-column end-column) := ($end-column $start-column);
    };
    if {$start-line = $end-line} {
	if {$start-column > $end-column} {
	    (start-column end-column) := ($end-column $start-column);
	};
    };
    if {$start-line < 0} {
	start-line := 0;
    };
    is-eof := <nil>;
    if {$end-line >= [self : len]} {
	end-line ::= self : len : - 1;
        is-eof := <t>;
	if {$end-line < 0} {
	    end-line := 0;
	};
	end-column ::= self : get [self : len : - 1] : len;
    };
    if {$start-line >= [self : len]} {
	return (<nil> 0 0 0);
    };
    if {$start-column > [self : get $start-line : len]} {
	start-column := [self : get $start-line : len];
    };
    if {$end-column > [self : get $end-line : len]} {
	end-column := [self : get $end-line : len];
    };
    at-last := <nil>;
    if {and {$end-line = [self : len : - 1]} {$end-column = [self : get $end-line : len]}} {
	at-last := <t>;
    };

    result-start-line := $start-line;
    result-start-column := $start-column;
    result-str := "";

    ## Region into same line.
    if {$start-line = $end-line} {
	r ::= self : get $start-line;
	s1 ::= $r sub 0 $start-column;
	str ::= $r sub $start-column $end-column;
	s2 ::= $r sub $end-column;
        self : set $start-line [$s1 . $s2];
	$result-str append! $str;
        if $is-eof {
            $result-str append! "\r";
        };
	if {and $at-last {[self : len] >= 1} {[self : get [self : len : - 1]] = ""}} {
	    self : resize [self : len : - 1];
	};
        return (insert-string-at $result-str $result-start-line $result-start-column);
    };

    ## Regin into 2 lines.
    if {$start-line = [$end-line - 1]} {
	s1 ::= self : get $start-line : sub 0 $start-column;
	$result-str append! [self : get $start-line : sub $start-column];
	$result-str append! "\r";
	$result-str append! [self : get $end-line : sub 0 $end-column];
        if $is-eof {
            $result-str append! "\r";
        };
	s2 ::= self : get $end-line : sub  $end-column;
        self : set $start-line [$s1 . $s2];
	if {[self : len : - 1] <= $end-line} {
	    self : resize [self : len : - 1];
	} {
            #$end-line each to: [self : len : - 2] do: {| i |
	    #    self : set $i [self : get [$i + 1]];
	    #};
	    #self : resize [self : len : - 1];
            self : delete! $end-line;
	};
	if {and $at-last {[self : len] >= 1} {[self : get [self : len : - 1]] = ""}} {
	    self : resize [self : len : - 1];
	};
	return (insert-string-at $result-str $result-start-line $result-start-column);
    };

    ## Regin over 3 lines.
    s1 ::= self : get $start-line : sub 0 $start-column;
    $result-str append! [self : get $start-line : sub $start-column];
    $result-str append! "\r";
    [$start-line + 1] each to: [$end-line - 1] do: {| i |
	$result-str append! [self : get $i];
	$result-str append! "\r";
    };
    $result-str append! [self : get $end-line : sub 0 $end-column];
    if $is-eof {
        $result-str append! "\r";
    };
    s2 ::= self : get $end-line : sub  $end-column;
    self : set $start-line [$s1 . $s2];
    n ::= $end-line - $start-line;
    #[$end-line + 1] each to: [self : len : - 1] do: {| i |
    #    if {$i >= [self : len]} {break};
    #    self : set [$i - $n] [self : get $i];
    #};
    #self : resize [self : len : - $n];
    1 each to: $n do: {
        self : delete! [$start-line + 1];
    };
    #if {$end-line >= [self : len]} {
    #    self : resize [self : len : - 1];
    #};
    
    if {and $at-last {[self : len] >= 1} {[self : get [self : len : - 1]] = ""}} {
        self : resize [self : len : - 1];
    };
    return (insert-string-at $result-str $result-start-line $result-start-column);
};

Vector method copy-region (start-line start-column end-line end-column) {
    if {$start-line > $end-line} {
	(start-line end-line) := ($end-line $start-line);
	(start-column end-column) := ($end-column $start-column);
    };
    if {$start-line = $end-line} {
	if {$start-column > $end-column} {
	    (start-column end-column) := ($end-column $start-column);
	};
    };
    if {$start-line < 0} {
	start-line := 0;
    };
    is-eof := <nil>;
    if {$end-line >= [self : len]} {
	end-line ::= self : len : - 1;
        is-eof := <t>;
	if {$end-line < 0} {
	    end-line := 0;
	};
	end-column ::= self : get [self : len : - 1] : len;
    };
    if {$start-line >= [self : len]} {
	return (<nil> "" 0 0);
    };
    if {$start-column > [self : get $start-line : len]} {
	start-column := [self : get $start-line : len];
    };
    if {$end-column > [self : get $end-line : len]} {
	end-column := [self : get $end-line : len];
    };

    result-str := "";

    ## Region into same line.
    if {$start-line = $end-line} {
	r ::= self : get $start-line;
	str ::= $r sub $start-column $end-column;
	$result-str append! $str;
        if $is-eof {
            $result-str append! "\r";
        };
	return (insert-string-at $result-str $start-line $start-column);
    };

    ## Regin into 2 lines.
    if {$start-line = [$end-line - 1]} {
	self ::= self;
	$result-str append! [$self get $start-line : sub $start-column];
	$result-str append! "\r";
	$result-str append! [$self get $end-line : sub 0 $end-column];
        if $is-eof {
            $result-str append! "\r";
        };
	return (insert-string-at $result-str $start-line $start-column);
    };

    ## Regin over 3 lines.
    $result-str append! [self : get $start-line : sub $start-column];
    $result-str append! "\r";
    [$start-line + 1] each to: [$end-line - 1] do: {| i |
	$result-str append! [self : get $i];
	$result-str append! "\r";
    };
    $result-str append! [self : get $end-line : sub 0 $end-column];
    if $is-eof {
	$result-str append! "\r";
    };
    return (insert-string-at $result-str $start-line $start-column);
};

Vector method each-forward-char (sl sc body) {
    self ::= self;
    to ::= $self len : - 1;
    if {$sl > $to} {return};
    src ::= [$self get $sl] . "\r";
    if {[$src len] > 0} {
        if {$sc < [$src len]} {
            $sc each to: [$src len : - 1] do: {| j |
                result := ([yield $body $sl $j [$src at $j]]);
                if [control? [$result car]] {return};
            };
        };
    };
    $sl ++;
    if {$sl > $to} {return};
    $sl each to: $to do: {| i |
        src ::= [$self get $i] . "\r";
        if {[$src len] > 0} {
            0 each to: [$src len : - 1] do: {| j |
                result := ([yield $body $i $j [$src at $j]]);
                if [control? [$result car]] {return};
            };
        };
    };
};

Vector method each-backward-char (sl sc body) {
    self ::= self;
    to := 0;
    if {[$self len] = 0} {return};
    if {$sl >= [$self len : - 1]} {sl ::= $self len : - 1};
    src ::= [$self get $sl] . "\r";
    if {[$src len] > 0} {
        if {$sc >= [$src len]} {sc ::= $src len : - 1};
        $sc each to: 0 do: {| j |
            result := ([yield $body $sl $j [$src at $j]]);
            if [control? [$result car]] {return};
        };
    };
    $sl --;
    if {$sl < 0} {return};
    $sl each to: $to do: {| i |
        src ::= [$self get $i] . "\r";
        if {[$src len] > 0} {
            [$src len : - 1] each to: 0 do: {| j |
                result := ([yield $body $i $j [$src at $j]]);
                if [control? [$result car]] {return};
            };
        };
    };
};

Vector method get-next-pos (y x) {
    if {[self : len] <= 0} {return (0 0)};
    
    $x ++;
    if {$y < [len]} {
        if {$y < 0} {
            return (0 0);
        };
        r ::= get $y;
        if {$x <= [$r len]} {return ($y $x)};
        $y ++;
        x := 0;
        return ($y $x);
    };
    
    y ::= len;
    return ($y 0);
};

Vector method get-prev-pos (y x) {
    $x --;
    if {$x < 0} {
        $y --;
        if {$y >= 0} {
            x ::= get $y : len;
        } {
            y := 0;
            x := 0;
        };
    };
    return ($y $x);
};

Vector method at (y x) {
    if {or {$y < 0} {$y >= [self : len]}} {return ""};
    return [get $y : at $x];
};

defun open-file (name) {
    path ::= path-adjust [
        {[$name at 0] = "/"} ???
            $name
            [pwd : . "/" $name]
    ];
    buff ::= new EditBuffer;
    (result err buff) ::= $buff load-content $path : <<- $buff;
};

defun get-current-buffer-dir (window) {
    pwd ::= pwd;
    if {$pwd = "/"} else: {pwd ::= $pwd . "/"};
    $window ?? {return $pwd};
    
    buff ::= $window get-buffer;
    $buff ?? {return $pwd};
    
    path ::= $buff get-path;
    $path ?? {return $pwd};
    
    if {file dir? $path} {
        if {$path = "/"} else: {
            if {$path at -1 : = "/"} else: {
                path ::= $path . "/";
            };
        };
        return $path;
    };

    dir ::= $path split sep: "/";
    $dir ->>;
    path ::= $dir join sep: "/";
    if {$path = "/"} else: {path ::= $path . "/"};
    return $path;
};

defun reset-stdio () {
    setvar stdin  [new Stream];
    $stdin set-nolimit;
    $stdin set-comode;

    setvar stdout [new Stream];
    $stdout set-limit 1024;
    $stdout set-itemlimit 80;
    $stdout set-comode;

    setvar stderr [new Stream];
    $stderr set-limit 1024;
    $stderr set-itemlimit 80;
    $stderr set-comode;
};

defun mode-select (window buffer-type buffer-name buffer-mode file-path) {
    suffix ::= if $file-path {$file-path split sep: "." : last : car};

    result := <nil>;
    if {! [nil? $buffer-mode]} {
        result := $buffer-mode;
    } {
        $MODE_SELECT_LIST each do: {| i |
            (cond mode) := $i;
            try {
                if [fun (window buffer-type buffer-name buffer-mode file-path suffix) $cond :
                         $window $buffer-type $buffer-name $buffer-mode $file-path $suffix] {
                    result := $mode;
                    break;
                };
            } catch: {
                show-message :bell ["Error at mode selector, mode name is " . $mode "."];
            };
        };
        $result ?? {result := $DEFAULT_MODE};
    };
    result $result;
};

defun read-user-conf-file (conf-name) {
    path ::= $USER_CONF_DIR . "/" $conf-name;
    if {and {file read? $path} {! [file dir? $path]}} {
        try {
            load :notrace $path;
        } catch: {
            | e |
            message ::= "User configuration file (" . $path ") has error, <" [$e car] "># " [$e cdr];
            try {
                $MainWindow add-background after: 100 {
                    show-message :bell $message;
                    result ALL;
                };
            } catch: {
                throw $e;
            };
        };
    };
};

defun regular-file? (path count: count) {
    [set? count] ?? {count := 0};
    $count ++;
    if {$count > 20} {
        throw ErrLinkLoop "Too many symbolic link.";
    };
    try {
        if {file stat $path : assoc-value type : = "-"} {return <t>};
        if {file stat $path : assoc-value type : = "l"} {
            next ::= file stat $path : assoc-value symbolic-link;
            if {$next at 0 : = "/"} {
                return [regular-file? count: $count $next];
            } {
                return [regular-file? count: $count [make-base $path : . "/" $next]];
            };
        };
    };
    return <nil>;
};

defun get-real-path (path) {
    result ::= get-real-path-sub $path count: 0;
    if {$path at 0 : != "/"} {
        pwd ::= pwd;
        if {$pwd = "/"} {
            result ::= path-adjust [$pwd . $result];
        } {
            result ::= path-adjust [$pwd . "/" $result];
        };
    };
    return $result;
};

defun get-real-path-sub (path count: count) {
    [set? count] ?? {count := 0};
    $count ++;
    if {$count > 20} {
        throw ErrLinkLoop "Too many symbolic link.";
    };
    try {
        if {file stat $path : assoc-value type : != "l"} {return $path};
        if {file stat $path : assoc-value type : = "l"} {
            next ::= file stat $path : assoc-value symbolic-link;
            if {$next at 0 : = "/"} {
                return [get-real-path-sub $next count: $count];
            } {
                return [get-real-path-sub [make-base $path : . "/" $next] count: $count];
            };
        };
    } catch: {| e |
        case [$e car]
        ErrFileAccess {
            return $path;
        };
        throw $e;
    };
    return <nil>;
};

defun make-base (path) {
    l ::= $path split sep: "/";
    $l ->>;
    result ::= $l join sep: "/";
    if {$result = ""} {
        if {$path at 0 : != "/"} {
            result := "."
        };
    };
    return $result;
};

defun get-path-condition (path) {
    ### returns: (stat original-path message)
    ### stat: REGULAR ..... Regular file
    ###       DIRECTORY ... Directory
    ###       NOEXISTS .... File not exists
    ###       NOPERM ...... File not permitted
    ###       SPACIAL ..... Special file
    ###       LOOP ........ Too many depth symbolic link

    try {
        path ::= get-real-path $path;
    } catch: {| e |
        case [$e car]
        ErrFileAccess {
            return (NOEXISTS $path [$path . " is not exists."]);
        }
        ErrLinkLoop {return (LOOP $path [$path . " symbolic link is too many depth."])}
        ;;
    };
        
    try {
        cur-type ::= file stat $path : assoc-value type;
    } catch: {
        base ::= make-base $path;
        if {file write? $base} {
            return (NOEXISTS $path [$path . " is not exists."]);
        } {
            return (NOPERM $base [$base . " directory is not permitted."]);
        };
    };

    case $cur-type
        "-" {
            if [file read? $path] {
                return (REGULAR $path [$path . " is regular file."]);
            } {
                return (NOPERM $path [$path . " is not permitted."]);
            };
        }
        "d" {
            if [file read? $path] {
                return (DIRECTORY $path [$path . " is directory."]);
            } {
                return (NOPERM $path [$path . " is not permitted."]);
            };
        }
        "b" {return (SPECIAL $path [$path . " is special file (block device)."])}
        "c" {return (SPECIAL $path [$path . " is special file (character device)."])}
        "p" {return (SPECIAL $path [$path . " is special file (pipe)."])}
        "s" {return (SPECIAL $path [$path . " is special file (socket)."])}
        default: {return (SPECIAL $path [$path . " is unknown type file."])}
    ;;
};

defun setconf (key value ro: read-only desc: description) {
    if {$CONFIG_RO get $key} {
        throw ErrConfig ["Configuration key '" . $key "' is read only."];
    };

    t ::= $CONFIG_TYPE get $key;
    if $t {
        if {! [eq? [type? $value] $t]} {
            throw ErrConfig ["Configuration key '" . $key "' type mismatch."];
        };
    } {
        $CONFIG_TYPE set $key [type? $value];
    };
    
    if {true? description} {
        $CONFIG_DESC set $key $description;
    };
    $CONFIG set $key $value;
    undef $key;
    defvar $key $value;

    if {true? read-only} {
        $CONFIG_RO set $key <t>;
        return [$CONFIG get $key];
    };
    
    return $value;
};

defun unsetconf (key) {
    $CONFIG unset $key;
    $CONFIG_RO unset $key;
    $CONFIG_TYPE unset $key;
};

defun mouse-on () {
    if [curs-mouse?] {
    	curs-mouse-on;
    };
};

defun mouse-off () {
    if [curs-mouse?] {
        curs-mouse-off;
    };
};
