class TaskManager;

TaskManager method init () {
    if {sets? TaskList} {
        $TaskList each do: {| i |
            try {$i release} catch: {};
        };
    };
    sets TaskList ();
    sets LastDispatch [time-of-day];
    
    sets Task_stdout [new EditStream];
    $Task_stdout open "*Console*";
    
    sets SysReq [new Stream];
    sets WaitHook [dict ()];
};

TaskManager method add (task name) {
    if {$task instance? Task} else: {
        throw ErrBadType "Not a Task object.";
    };
    if {$TaskList find $task} {
        throw ErrDuplicateTask "Already registerd task.";
    };
    n := 1;
    name ::= "" . $name;
    sname := $name;
    tlist ::= $TaskList map {| i | $i get-name};
    while {$tlist find $sname} do: {
        n ::= $n + 1;
        sname ::= $name . ":" $n;
    };
    $TaskList <<- $task;
    $task set-name $sname;
    in ::= new Stream;
    $task set-stdio $in $Task_stdout $Task_stdout;
    return $sname;
};

TaskManager method get (name) {
    $TaskList each do: {| i |
        if {[$i get-name] = $name} {
            return $i;
        };
    };
    return <nil>;
};

TaskManager method get-task-list () {
    return $TaskList;
};

TaskManager method get-task (name) {
    $TaskList each do: {| t |
        if {$t get-name : = $name} {
            return $t;
        };
    };
    return <nil>;
};

TaskManager method dispatch () {
    now ::= time-of-day :msec;
    tick ::= $now - $LastDispatch;
    sets LastDispatch $now;
    
    min-wait := 0;
    min-task := <nil>;
    $TaskList each do: {| t |
        if {$t tickable?} {
            wait ::= $t time-elapsed $tick;
            
            if {$wait <= 0} {
                if {$wait < $min-wait} {
                    min-wait := $wait;
                    min-task := $t;     # run task candidate
                };
            };
        };
    };
    result := <nil>;
    $SysReq clear;
    $SysReq clear-queue;
    if $min-task {
        $min-task run;     # dispatch to min task
        if {$min-task exit?} {
            $Task_stdout puts "";
            $Task_stdout puts ["Task [" . [$min-task get-name] "] end, status= " [$min-task get-exit-status]];
            idx ::= $TaskList find $min-task;
            if {$idx >= 0} {
                $TaskList delete! $idx
            };
        };
        result := <t>;
        $SysReq close;
        apply-syscalls;
    };
    
    try {
        force-wait-task;
    } catch: {| e |
        $Task_stdout puts ["Error at force-wait-task: " . $e];
    };
    
    return $result;
};

TaskManager method enqueue (msg) {
    $SysReq puts $msg;
};

TaskManager method apply-syscalls () {
    while {! [$SysReq eof?]} do: {
        r ::= $SysReq gets;
        if {list? $r} {
            case [$r car] 
            APPLY_EDITOR {
                $r,1 eval;
            }
            default: {
                $Task_stdout puts ["Bad SysReq(APPLY_EDITOR): " . $r];
            }
        } {
            $Task_stdout puts ["Bad SysReq(UNKNOWN): " . $r];
        };
    };
};

TaskManager method force-wait-task () {
    $WaitHook keys : each do: {| name |
        try {
            (task file checker-block) ::= $WaitHook get $name;
            if {! [eq? [$task get-state] W]} {
                continue;
            };
            if {yield $checker-block} {
                $task set-sleep;
            };
        }
        catch: {| e |
            $Task_stdout puts ["Bad SysReq(GETS/PUTS): " . $e ", " $name];
        };
    };
};

TaskManager method add-wait-hook (task-name wait-object) {
    $WaitHook set $task-name $wait-object;
};

TaskManager method delete-wait-hook (task-name) {
    $WaitHook unset $task-name;
};
    
TaskManager method panic (msg) {
    $Task_stdout puts "";
    $Task_stdout puts $msg;
};

