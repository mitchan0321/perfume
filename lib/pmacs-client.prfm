##
## exit status:
##    0 ... normal end
##    1 ... disconnect from server
##    2 ... bad protocol
##    3 ... server down
##    4 ... bad key (from server)
##

defun pmacs-client () {
    pmacs-server-config;
    
    files ::= $ARGV clone;
    $files >>;
    if {and {! [$files null?]} {$files,0 = "-c"}} {
        $files >>; $files >>;
    };

    sts := 0;
    $files each do: {| f |
        if {$f at 0 : != "/"} {
            f ::= [pwd] . "/" $f;
        };
        sts ::= pmacs-client-sub $f;
    };

    exit $sts;
};

defun pmacs-client-sub (path) {
    if {defvar? PMACS} {
        println "Can't run under Pmacs environment."; #'
        return;
    };

    try {
        f ::= connect [resolv-host "localhost"] $PMACS-SERVER-PORT;
    } catch: {
        return 3; ## not connect
    };
    
    server ::= new File;
    $server set! $f mode: io;
    $server set-encoding UTF-8;
    $server set-newline <t>;
    $server set-readbuffer-max 4096;

    sts := 1; ## disconnect from server
    try {
        $server puts $PMACS-SERVER-KEY;
        r ::= $server gets;
        if {nil? $r} {
            return 1; ## disconnect from server
        };
        try {
            sts ::= $r clean : int;
        } catch: {
            return 2; ## protocol error
        };
        if {$sts != 0} {return $sts};
        
        $server puts $path;
        r ::= $server gets;
        if {nil? $r} {
            return 1; ## disconnect from server
        };
        try {
            sts ::= $r clean : int;
        } catch: {
            return 2; ## protocol error
        };
    } fin: {
        $server close;
    };
    
    return $sts;
};

