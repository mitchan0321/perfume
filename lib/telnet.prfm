defun telnet (host enc: enc port: port) {

    if [set? enc] else: {enc := Shift-JIS};
    if [set? port] else: {port := 23};
    
    f ::= connect [resolv-host $host] $port;
    server ::= new File;
    $server set! $f mode: io;
    $server set-encoding $enc;
    $server set-newline <nil>;
    $server set-ignore-cr <t>;
    
    try {
        loop do: {
	    if {$stdin ready?} {
	        message ::= $stdin gets;
	        if [$stdin eof?] then: {
		    println "Telnet client exit.";
		    return;
		};
		$server puts [$message . "\r"];
	    };
	    if {$server ready?} {
	        message ::= $server gets;
	        if [nil? $message] then: {
		    println "Disconnect from telnet server.";
                    return;
		};
		println $message;
	    };
	};
    }
    fin: {
	$server close;
    };
};

