defun telnet (host enc: enc port: port) {

    if [set? enc] else: {enc := Shift-JIS};
    if [set? port] else: {port := 23};
    
    f ::= connect [resolv-host $host] $port;
    server ::= new File;
    $server set! $f mode: io;
    $server set-output-encoding RAW;
    $server set-newline <t>;
    $server set-rawio <t>;
    
    imessage := "";
    try {
        loop do: {
	    if {$stdin ready?} {
                try {
                    omessage ::= $stdin gets;
                } catch: {
                    println "\nBad encoding input from stdin.";
                    continue;
                };
	        if [nil? $omessage] then: {
		    println "\nTelnet client exit.";
		    return;
                };
		$server puts [$omessage uencode [symbol ["" . $enc "/F"]]];
                $server flush;
            };
            
	    if {$server ready? timeout: 10} {
	        message ::= $server gets;
	        if [nil? $message] then: {
                    if {$imessage != ""} {
                        try {
                            o ::= $imessage udecode [symbol ["" . $enc "/F"]];
                            $stdout puts $o;
                            $stdout flush;
                        } catch: {};
                    };
                    println "\nDisconnect from telnet server.";
                    return;
                };
                $imessage append! $message;
                try {
                    o ::= $imessage udecode $enc;
                } catch: {| e |
                    if {eq? [$e car] ErrBadEncodeLessLength} {
                        continue;
                    };
                    o ::= $imessage udecode [symbol ["" . $enc "/F"]];
                    $stdout puts :nonewline ["\n[Decode error: " . $enc "] " $o "\n"];
                    $stdout flush;
                    imessage := "";
                    continue;
                };
                $stdout puts :nonewline $o;
                $stdout flush;
                imessage := "";
	    };
	};
    } catch: {| e |
        println "Error occured: #<" [$e car] "> " [$e cdr];
    } fin: {
	$server close;
    };
};

