defun telnet (host enc: enc port: port) {

    if [set? enc] else: {enc := Shift-JIS};
    if [set? port] else: {port := 23};
    
    f ::= connect [resolv-host $host] $port;
    server ::= new File;
    $server set! $f mode: io;
    $server set-rawio <t>;
    $stdin set-rawio <t>;
    
    omessage := "";
    imessage := "";
    try {
        loop do: {
	    if {$stdin ready?} {
	        message ::= $stdin gets;
	        if [nil? $message] then: {
		    println "Telnet client exit.";
		    return;
                };
                $omessage append! $message;
                try {
                    o ::= $omessage udecode UTF-8/F;
                } catch: {| e |
                    if {eq? [$e car] ErrBadEncodeLessLength} {
                        continue;
                    };
                    println "Decode error: " $omessage;
                    omessage := "";
                    continue;
                };
		$server puts $o;
                $server flush;
                omessage := "";
	    };
	    if {$server ready? timeout: 10} {
	        message ::= $server gets;
	        if [nil? $message] then: {
                    println "Disconnect from telnet server.";
                    return;
                };
                $imessage append! $message;
                try {
                    o ::= $imessage udecode $enc;
                } catch: {| e |
                    if {eq? [$e car] ErrBadEncodeLessLength} {
                        continue;
                    };
                    println "Decode error: " $imessage;
                    imessage := "";
                    continue;
                };
                
                $stdout puts :nonewline $o;
                imessage := "";
	    };
	};
    }
    fin: {
	$server close;
    };
};

