###
### Pmacs editor document command definition.
###

defcommand pwiki-next-link desc: "Jump to next PWiki anchor link." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    (ey ex) ::= $window get-source-index $buff $geom;
    (y x) ::= get-next-search $ey $ex $PWIKI_LINK $buff;
    $window cursor-move-to $geom $y $x;
    return SELF;
};

defcommand pwiki-previous-link desc: "Jump to previous PWiki anchor link." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    (ey ex) ::= $window get-source-index $buff $geom;
    (y x) ::= get-previous-search $ey $ex $PWIKI_LINK $buff;
    $window cursor-move-to $geom $y $x;
    return SELF;
};

defcommand pwiki-click-link desc: "Open PWiki document." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    (ey ex) ::= $window get-source-index $buff $geom;
    sts ::= pwiki-open-link $main $window $buff $geom $ey $ex;
    if $sts else: {
        $window apply-command $main $window "\r" insert-character;
    };
    return ALL;
};

defun pwiki-open-link (main window buff geom ey ex) {
    str ::= cursor-on-search-regex $ey $ex $PWIKI_LINK $buff;
    $str ?? {
        return <nil>;
    };

    link ::= $str sub 2 [$str len : - 2];
    if {[$link at 0] = "&"} {
        ### if pwiki link form is [[&command]], execute command.
        command ::= $link sub 1;
        $window apply-command $main $window "" [symbol $command];
        return <t>;
    };
    if {[$link at 0] = "?"} {
        ### if pwiki link form is [[?command]], show-keybindings.
        command ::= $link sub 1;
        result ::= get-keybind-string $window $command;
        if {$result = ""} {
            if {get-command $command} {
                show-message ["No binding key for: " . $command];
            } {
                show-message ["No defined command: " . $command];
            };
        } {
            show-message [$result . " -> " $command];
        };
        return <t>;
    };
    
    if {[$link at 0] = "@"} {
        ### if pwiki link form is [[@URL]], execute Web browser.
        url ::= $link sub 1;
        $WEB_BROWSER_LIST each do: {| exec |
            if {file exec? $exec} {
                do-command $exec "WEB-BROWSER" ($url);
                break;
            };
        };
        return <t>;
    };
    
    path ::= $buff get-content;
    if {[$link at 0] = "/"} {
        dir := "";
    } {
        if $path {
            dir ::= get-dir $path;
        } {
            dir := $DEFAULT_PWIKI_DIR;
        };
    };
    
    new-path ::= [$dir = ""] ??? [$link . ".pwiki"] [path-adjust [$dir . $link ".pwiki"]];
    next-buff ::= BufferManager get-buffer-by-path $new-path;
    $next-buff ?? {
        next-buff ::= new EditBuffer;
        create-directory-if-not-exists $new-path;
        (result err) ::= $next-buff load-content $new-path;
        if {$next-buff enc-error?} {
            show-message :bell "File decode error occured!! Try ALT-e and specify another encoding for re-load.";
        };
        $result ?? {
            show-message :bell ["Open Failed, path: " . $new-path " ("  $err ")"];
            return <t>;
            if {$buff enc-error?} {
                show-message :bell "File decode error occured!! Try ALT-e and specify another encoding for re-load.";
            };
        };
    };
    $main bind-buffer [$window get-window-name] $next-buff;
    $next-buff set-previous-buffer [$buff get-buffer-name];
    
    return <t>;
};

defun do-command (command task-name params) {
    ###
    ### do command
    ###
    task ::= new Task;
    $task proc ["do-command:" . $task-name] {
        (command params) := $ARGS;
        cmdline ::= [$command literal] . [$params inject "" do: {| s i | $s . [" " . [$i literal]]}];
        println "[" [get-name] "] command line: " $cmdline;
        sts ::= exec "/bin/sh" "-c" $cmdline;
    } $command $params;

    return;
};

defun get-keybind-string (window command) {
    keys ::= $window get-key-bindings $command;
    if {$keys null?} {
        return "";
    };
    keys ::= $keys map {| i |
        if {list? $i} {
            $i map {| j | symbol [getkeydesc $j]};
        } {
            symbol [getkeydesc $i];
        };
    };
    if {$keys len : = 1} {
        return ["" . $keys,0];
    };
    result := "";
    0 each to: [$keys len : - 2] do: {| i |
        $result append! $keys,$i;
        $result append! " , ";
    };
    $result append! [$keys get [$keys len : - 1]];
    return $result;
};

defun create-directory-if-not-exists (path) {
    l ::= $path split sep: "/";
    $l ->>;
    if {[$path at 0] = "/"} {$l >>};
    dir := "";
    $l each do: {| e |
        $dir append! "/";
        $dir append! $e;
        if {file dir? $dir} {continue};
        try {
            file mkdir $dir $DEFAULT_DIR_MODE;
        } catch: {| err |
            throw ErrFileAccess ["Can\'t create directory \'" . $dir "\'."]; #'
        };
    };
};

defcommand pwiki-link-back desc: "Back to previous PWiki document." {
    | main window |
    buff ::= $window get-buffer;
    prev-buff-name ::= $buff get-previous-buffer;
    if $prev-buff-name {
        prev-buff ::= BufferManager find-buffer $prev-buff-name;
        if $prev-buff {
            $main bind-buffer [$window get-window-name] $prev-buff;
        } {
            show-message :bell ["No found previous buffer, \'" . $prev-buff-name "\'."];
        };
    } {
        show-message :bell "No set previous buffer.";
    };
    result ALL;
};

defun cursor-on-search-regex (y x str buff) {
    r ::= $buff get-buffer-line $y;
    $r ?? {return <nil>};
    sl ::= $r =~ :all $str;
    $sl ?? {return <nil>};
    $sl each do: {| el |
        if {and {$x >= [$el get 0]} {$x < [$el get 1]}} {
            return [$el get 2];
        };
    };
    return <nil>;
};

defcommand man desc: "Open man document on the entered text." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    str := "";
    if [$window get-region] {
        (sy sx) ::= $window end-region;
        (ey ex) ::= $window get-source-index $buff $geom;
        str ::= $window get-region-string $sy $sx $ey $ex;
    };

    mini-input-until-enter $main $window
        init-value: $str
        prompt: "Man topic: "
        enter-proc: {| value |
            if {$value = ""} {
                show-message :bell "Enter man topic name.";
                return ALL;
            };
            exec-man $main $window $buff $value :stay;
            return ALL;
        }
    ;;
    
    return ALL;
};

defcommand man-current-word desc: "Open man document on near cursor point word." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;

    orig_geom ::= $geom clone;
    $window apply-command $main $window "" select-region-word;

    [$window in-region?] ?? {
        show-message "Word not found.";
        return ALL;
    };

    (sy sx) ::= $window end-region;
    (ey ex) ::= $window get-source-index $buff $geom;
    str ::= $window get-region-string $sy $sx $ey $ex;
    $geom restore-geom $orig_geom;
    exec-man $main $window $buff $str;
    return ALL;
};

defun exec-man (main window cur-buff topic stay: stay) {
    ltopic ::= $topic split;
    buff-name ::= if {$ltopic len : >= 2} {"Man=" . $ltopic,1 "(" $ltopic,0 ")"} {"Man=" . $topic};
    man-buff ::= BufferManager find-buffer $buff-name;
    if {and {true? stay} $man-buff} {
        $main bind-buffer [$window get-window-name] $man-buff;
    } {
        if {true? stay} {
            do-man-async $window $buff-name $cur-buff $ENV,HOME [$MAN_PREFIX expand] $MAN_ENCODING stay: <t>;
        } {
            do-man-async $window $buff-name $cur-buff $ENV,HOME [$MAN_PREFIX expand] $MAN_ENCODING stay: <nil>;
       };
    };
    return ALL;
};

defun do-man-async (window buff-name cur-buff directory command enc stay: stay) {
    BufferManager unregist-buffer $buff-name;
    man-buff ::= new EditStream;
    $man-buff open $buff-name;
    _puts ::= $man-buff method? puts;
    cnt := 50;
    $man-buff method puts (*) {
        r ::= call $_puts ([curs-col [$* join sep: ""]]);
        $cnt --;
        if {$cnt = 0} {
            # early display man header
            $MainWindow set-force-update;
        };
        return $r;
    };
    $man-buff get-buffer : set-content $directory;
    $man-buff set-columnlimit 256;
    if {true? stay} {
        $MainWindow bind-buffer [$window get-window-name] [$man-buff get-buffer];
    } {
        if [$MainWindow get-window-list : len : <= 1] {
            $MainWindow create-split-vertical-window window-name: [$window get-window-name];
            $MainWindow bind-buffer [$window get-window-name] $cur-buff;
            $MainWindow bind-buffer [$MainWindow next-visible-window $window : get-window-name] [$man-buff get-buffer];
        } {
            $MainWindow bind-buffer [$MainWindow next-visible-window $window : get-window-name] [$man-buff get-buffer];
        };
    };
    
    task ::= new Task;
    $task set-stdout $man-buff;
    $task set-stderr $man-buff;
    $task set-terminate-proc {
        $MainWindow set-force-update;
    };
    
    ###
    ### do command
    ###
    $task proc man-task {
        (window buff cmd directory enc) := $ARGS;
        cd $directory;
        sts ::= exec err-to: [new NullStream] proc-enc: $enc sh -c $cmd;
        println "";
        println "Exit man command with status " $sts ".";
        
        SysCall apply-editor {
            $buff set-protect;
            $buff no-modify;
            if {eq? [$window get-buffer] $buff} {
                geom ::= $window get-geom;
                $geom set! ViewLine 0;
                $geom set! ViewColumn 0;
                $window cursor-move-to $geom 0 0;
            };
        };
        
    } $window [$man-buff get-buffer] $command $directory $enc;

    return;
};

defcommand man-click-link desc: "Open man document." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    (ey ex) ::= $window get-source-index $buff $geom;
    sts ::= man-open-link $main $window $buff $geom $ey $ex;
    if $sts else: {
        show-message :bell "No man topic.";
    };
    result ALL;
};

defun man-open-link (main window buff geom ey ex) {
    str ::= cursor-on-search-regex $ey $ex $MAN_LINK $buff;
    $str ?? {
        return <nil>;
    };

    buff ::= $window get-buffer;
    buff-name ::= $buff get-buffer-name;
    
    topic ::= $str split sep: "(" : get 0 : lower;
    section ::= $str split sep: "(" : get 1 : at 0;
    show-message ["Man topic: " . $section " " $topic];
    exec-man $main $window $buff [$section . " " $topic] :stay;

    next-buff ::= $window get-buffer;
    $next-buff set-previous-buffer $buff-name;

    return <t>;
};

defcommand man-link-back desc: "Back to previous man document." {
    | main window |
    buff ::= $window get-buffer;
    prev-buff-name ::= $buff get-previous-buffer;
    if $prev-buff-name {
        prev-buff ::= BufferManager find-buffer $prev-buff-name;
        if $prev-buff {
            $main bind-buffer [$window get-window-name] $prev-buff;
        } {
            show-message :bell ["No found previous buffer, \'" . $prev-buff-name "\'."];
        };
    } {
        show-message :bell "No set previous buffer.";
    };
    result ALL;
};

defcommand man-next-link desc: "Jump to next man link." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    (ey ex) ::= $window get-source-index $buff $geom;
    (y x) ::= get-next-search $ey $ex $MAN_LINK $buff;
    $window cursor-move-to $geom $y $x;
    return SELF;
};

defcommand man-previous-link desc: "Jump to previous man link." {
    | main window |
    buff ::= $window get-buffer;
    geom ::= $window get-geom;
    (ey ex) ::= $window get-source-index $buff $geom;
    (y x) ::= get-previous-search $ey $ex $MAN_LINK $buff;
    $window cursor-move-to $geom $y $x;
    return SELF;
};

defcommand help desc: "Open pmacs manual." {
    | main window |
    cur-buff ::= $window get-buffer;
    help-path ::= $DEFAULT_PWIKI_DIR . $DEFAULT_PWIKI_HOME_FILE;
    buff ::= BufferManager get-buffer-by-path $help-path;
    $buff ?? {
        buff ::= new EditBuffer;
        create-directory-if-not-exists $help-path;
        (result err) ::= $buff load-content $help-path;
        $result ?? {
            show-message :bell ["Open Failed, path: " . $help-path " ("  $err ")"];
            return SELF;
        };
    };
    $main bind-buffer [$window get-window-name] $buff;
    $buff set-previous-buffer [$cur-buff get-buffer-name];

    return ALL;
};
