defun telnet-server (file: file port: port test: test) {
    
    pid-list := ();

    if [set? file] else: {file := "/dev/null"};
    if [set? port] else: {port := 8023};
    if [set? test] else: {test := <nil>};
    
    # create listen port

    lfd ::= socket-server $port;

    try {
	println "Listening file descriptor: " $lfd;
	client-list ::= dict;
	client-addr ::= dict;
    
	loop do: {

            pid-list ::=
                $pid-list filter {| p |
                    try {
                        (rpid sts) ::= wait :noblock $p;
                    } catch: {
                        rpid := -1;
                    };
                    {$rpid = 0} ??? <t> <nil>;
                }
            ;;

	    # check listen port for connect request from client

	    (ifds ofds efds) ::= select ($lfd) () () 10;

	    if [$ifds null?] else: {

	        # proseed client connect request

		(fd addr port) ::= accept $lfd;
		f ::= new File;
		$f set! $fd mode: io;
		$f set-encoding RAW;
		$f set-newline <t>;
		println "Connect client from " [host-address-string $addr] ":" $port;
                telnet-server-send-file $f $file $pid-list $test;
            };
        };
    }
    fin: {
	close $lfd
    };
};

defun telnet-server-send-file (sock file pid-list test) {
    pid ::= fork;
    if {$pid != 0} {
        $pid-list <<- $pid;
        $sock close;
        return;
    };
    f ::= new File init: ($file mode: i);
    $f set-encoding RAW;
    $f each do: {| r |
        if $test {
            while {$r len : > 0} do: {
                o ::= $r sub 0 17;
                r ::= $r sub 17;
                $sock puts :nonewline $o;
                $sock flush;
                sleep 10;
            };
            $sock puts "\r";
        } {
            $sock puts [$r . "\r"];
        };
    };
    $sock close;
    $f close;
    exit 0;
};
