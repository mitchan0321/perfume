class EditBuffer BufferBase;

EditBuffer method init () {
    init-constructor;
};

EditBuffer method init-constructor () {
    sets Src <nil>;
    sets File <nil>;
    sets BufferName <nil>;
};

EditBuffer method load-content (path) {
    f := `File($path mode: i);
    sets Src [vector];
    try {
	$f each do: {| r |
	    $Src append! $r;
	};
    }
    fin: {
	$f close;
    };

    sets File $path;
    BufferManager regist-buffer [self] [$path split sep: "/" : last : car];
};

EditBuffer method buffer-select (from to block) {
    $from each to: $to do: {| i |
	if {and [$i < [$Src len]] [$i >= 0]}
	then: {
	    yield $block $Src,$i;
	}
	else: {
	    yield $block "";
	};
    };
};

EditBuffer method get-buffer-line (line) {
    $Src ?? {return <nil>};
    if {$line >= [$Src len]} {
	return <nil>;
    } {
	return [$Src get $line];
    }
};

EditBuffer method get-buffer-length () {
    return [$Src ??? [$Src len] 0];
};

EditBuffer method insert-string-at (input y x) {
    if {true? Src} {
	return [$Src insert-string-at $input $y $x];
    };
    return (0 0);
};

EditBuffer method delete-character-at (y x) {
    if {true? Src} {
	return [$Src delete-character-at $y $x];
    };
    return (0 0 "");
};

EditBuffer method backdelete-character-at (y x) {
    if {true? Src} {
	return [$Src backdelete-character-at $y $x];
    };
    return (0 0 "");
};

EditBuffer method append-string (str) {
    if {true? Src} {
	return [$Src append-string $str];
    };
    return (0 0 "");
};
